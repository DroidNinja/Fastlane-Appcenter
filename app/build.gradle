apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    defaultConfig {
        applicationId "com.droidninja.fastlanedemo"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode rootProject.ext.getBuildProperty("VERSION_CODE", rootProject.ext.versionCode as String) as int
        versionName rootProject.ext.getBuildProperty("VERSION_NAME", rootProject.ext.versionName)

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            def signingConfig = rootProject.ext.getSigningConfig('keystores/release.properties')
            storeFile signingConfig.storeFile
            storePassword signingConfig.storePassword
            keyAlias signingConfig.keyAlias
            keyPassword signingConfig.keyPassword
        }
        debug {
            def signingConfig = rootProject.ext.getSigningConfig('keystores/debug.properties')
            storeFile signingConfig.storeFile
            storePassword signingConfig.storePassword
            keyAlias signingConfig.keyAlias
            keyPassword signingConfig.keyPassword
        }
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        qa {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-qa"
            signingConfig signingConfigs.debug
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
            versionNameSuffix "-dev"
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        def isDebug = variant.buildType.name == "debug"
        //renaming qa and release build apks
        if (!isDebug) {
            println("----------------------------------------------")
            println("Variant name:      " + variant.name)
            println("Application ID:    " + [variant.mergedFlavor.applicationId, variant.buildType.applicationIdSuffix].findAll().join())
            println("Build type:        " + variant.buildType.name)

            variant.outputs.all {
                def versionName = variant.versionName
                def versionNumber = variant.versionCode.toString()
                def fileExtension = outputFileName.toString().substring(outputFileName.toString().lastIndexOf(".") + 1)
                def newName = "app_${versionNumber}_${versionName}.${fileExtension}"
                outputFileName = newName
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation rootProject.ext.deps.sdk.kotlin
    implementation rootProject.ext.deps.support.coreKtx
    implementation rootProject.ext.deps.support.recyclerview
    implementation rootProject.ext.deps.support.appCompat
    implementation rootProject.ext.deps.support.constraintLayout
    implementation rootProject.ext.deps.support.design
    implementation rootProject.ext.deps.support.navigationFragment
    implementation rootProject.ext.deps.support.navigationUIKtx
    implementation rootProject.ext.deps.fragment.runtime_ktx
    implementation rootProject.ext.deps.arch.lifecycleExtensions
    implementation rootProject.ext.deps.arch.lifecycleRuntime
    implementation rootProject.ext.deps.arch.lifecycleCommonJava

    testImplementation rootProject.ext.deps.test.junit
    androidTestImplementation rootProject.ext.deps.test.testRunner
    androidTestImplementation rootProject.ext.deps.test.espressoCore

}